#+title: logos.el: simple focus mode for Emacs with page breaks or outlines
#+author: Protesilaos Stavrou
#+email: info@protesilaos.com
#+language: en
#+options: ':t toc:nil author:t email:t num:t
#+startup: content
#+macro: stable-version 1.1.0
#+macro: release-date 2023-06-20
#+macro: development-version 1.2.0-dev
#+export_file_name: logos.texi
#+texinfo_filename: logos.info
#+texinfo_dir_category: Emacs misc features
#+texinfo_dir_title: Logos: (logos)
#+texinfo_dir_desc: Simple focus mode with page breaks or outlines
#+texinfo_header: @set MAINTAINERSITE @uref{https://protesilaos.com,maintainer webpage}
#+texinfo_header: @set MAINTAINER Protesilaos Stavrou
#+texinfo_header: @set MAINTAINEREMAIL @email{info@protesilaos.com}
#+texinfo_header: @set MAINTAINERCONTACT @uref{mailto:info@protesilaos.com,contact the maintainer}

#+texinfo: @insertcopying

This manual, written by Protesilaos Stavrou, describes the customization
options for =logos= (or =logos.el=), and provides every other piece of
information pertinent to it.

The documentation furnished herein corresponds to stable version
{{{stable-version}}}, released on {{{release-date}}}.  Any reference to
a newer feature which does not yet form part of the latest tagged
commit, is explicitly marked as such.

Current development target is {{{development-version}}}.

+ Package name (GNU ELPA): =logos=
+ Official manual: <https://protesilaos.com/emacs/logos>
+ Change log: <https://protesilaos.com/emacs/logos-changelog>
+ Git repositories:
  + GitHub: <https://github.com/protesilaos/logos>
  + GitLab: <https://gitlab.com/protesilaos/logos>
+ Video demo: <https://protesilaos.com/codelog/2022-03-11-emacs-logos-demo/>
+ Backronyms: =^L= Only Generates Ostensible Slides; Logos Optionally Goes through Outline Sections.

#+toc: headlines 8 insert TOC here, with eight headline levels

* COPYING
:PROPERTIES:
:COPYING: t
:CUSTOM_ID: h:efc32d6b-9405-4f3c-9560-3229b3ce3866
:END:

Copyright (C) 2022-2023  Free Software Foundation, Inc.

#+begin_quote
Permission is granted to copy, distribute and/or modify this document
under the terms of the GNU Free Documentation License, Version 1.3 or
any later version published by the Free Software Foundation; with no
Invariant Sections, with the Front-Cover Texts being “A GNU Manual,” and
with the Back-Cover Texts as in (a) below.  A copy of the license is
included in the section entitled “GNU Free Documentation License.”

(a) The FSF’s Back-Cover Text is: “You have the freedom to copy and
modify this GNU manual.”
#+end_quote

* Overview
:PROPERTIES:
:CUSTOM_ID: h:77518cc5-a7f4-4c5e-9c0f-7cf0f43b6317
:END:

This package provides a simple "focus mode" which can be applied to any
buffer for reading, writing, or even doing a presentation.  The buffer
can be divided in pages using the ~page-delimiter~, outline structure,
or any other pattern.  Commands are provided to move between those
pages.  These motions work even when narrowing is in effect (and they
preserve it).  =logos.el= is designed to be simple by default and easy
to extend.  This manual provides concrete examples to that end.

#+vindex: logos-outlines-are-pages
#+vindex: logos-outline-regexp-alist
What constitutes a page delimiter is determined by the user options
~logos-outlines-are-pages~ and ~logos-outline-regexp-alist~.  By
default, this only corresponds to the =^L= character (which can be
inserted using the standard keys with =C-q C-l=).

#+findex: logos-forward-page-dwim
#+findex: logos-backward-page-dwim
#+findex: logos-narrow-dwim
Logos does not define any key bindings.  Try something like this, if you
want:

#+begin_src emacs-lisp
(let ((map global-map))
  (define-key map [remap narrow-to-region] #'logos-narrow-dwim)
  (define-key map [remap forward-page] #'logos-forward-page-dwim)
  (define-key map [remap backward-page] #'logos-backward-page-dwim))
#+end_src

On standard Emacs, those key bindings are: =C-x n n=, =C-x ]=, =C-x [=.
The ~logos-narrow-dwim~ is not necessary for users who already know how
to narrow effectively.  Such users may still want to bind it to a key.

#+vindex: logos-repeat-map
For users running Emacs version 28 or higher, Logos defines the
~logos-repeat-map~ which is activated when ~repeat-mode~ is enabled.
This means that page motions, =C-x ]= and =C-x [=, can be repeated by
following them up with either =]= or =[=.  The repetition stops when
another command is invoked.

#+findex: logos-focus-mode
#+vindex: logos-hide-cursor
#+vindex: logos-hide-mode-line
#+vindex: logos-hide-header-line
#+vindex: logos-scroll-lock
#+vindex: logos-variable-pitch
#+vindex: logos-hide-buffer-boundaries
#+vindex: logos-buffer-read-only
#+vindex: logos-olivetti
#+vindex: logos-hide-fringe
Logos provides some optional aesthetic tweaks which come into effect
when the buffer-local ~logos-focus-mode~ is enabled. These will hide
the cursor (~logos-hide-cursor~), hide the mode line
(~logos-hide-mode-line~), hide the header line
(~logos-hide-header-line~), disable the buffer boundary indicators
(~indicate-buffer-boundaries~), enable ~scroll-lock-mode~
(~logos-scroll-lock~), use ~variable-pitch-mode~ in non-programming
buffers (~logos-variable-pitch~), make the buffer read-only
(~logos-buffer-read-only~), center the buffer in its window if the
=olivetti= package is installed (~logos-olivetti~), and hide the
~fringe~ face (~logos-hide-fringe~). All these variables are
buffer-local.

#+vindex: logos-focus-mode-map
Furthermore, the ~logos-focus-mode~ establishes a bespoke keymap, which
can be used to, for example, bind the arrow keys to page motions.  The
keymap is ~logos-focus-mode-map~ and is empty by default (we do not
define any keys and trust the user to pick their own).

Logos is the familiar word derived from Greek (watch my presentation on
philosophy about Cosmos, Logos, and the living universe:
<https://protesilaos.com/books/2022-02-05-cosmos-logos-living-universe/>),
though it also stands for these two perhaps equally insightful
backronyms about the mechanics of this package:

1. =^L= Only Generates Ostensible Slides
2. Logos Optionally Goes through Outline Sections

* Installation
:PROPERTIES:
:CUSTOM_ID: h:b0e78fe5-4e74-4959-be6f-10f7b631d5a0
:END:
#+cindex: Installation instructions

** GNU ELPA package
:PROPERTIES:
:CUSTOM_ID: h:35926213-4825-4933-932c-5183628d25ac
:END:

The package is available as =logos=.  Simply do:

: M-x package-refresh-contents
: M-x package-install

And search for it.

GNU ELPA provides the latest stable release.  Those who prefer to follow
the development process in order to report bugs or suggest changes, can
use the version of the package from the GNU-devel ELPA archive.  Read:
https://protesilaos.com/codelog/2022-05-13-emacs-elpa-devel/.

** Manual installation
:PROPERTIES:
:CUSTOM_ID: h:d9184e23-5a20-4efa-a8c4-1dfdd1e23dbe
:END:

Assuming your Emacs files are found in =~/.emacs.d/=, execute the
following commands in a shell prompt:

#+begin_src sh
cd ~/.emacs.d

# Create a directory for manually-installed packages
mkdir manual-packages

# Go to the new directory
cd manual-packages

# Clone this repo, naming it "logos"
git clone https://git.sr.ht/~protesilaos/logos logos
#+end_src

Finally, in your =init.el= (or equivalent) evaluate this:

#+begin_src emacs-lisp
;; Make Elisp files in that directory available to the user.
(add-to-list 'load-path "~/.emacs.d/manual-packages/logos")
#+end_src

Everything is in place to set up the package.

* Sample configuration
:PROPERTIES:
:CUSTOM_ID: h:6ed7f2fd-faad-48c9-bdd2-7e909639128d
:END:
#+cindex: Package configuration

Logos does not bind its own keys and does not make any opinionated
changes out-of-the-box ([[#h:2bb57369-352a-43bf-afe3-0bed2fcc7359][Extra tweaks]]):

+ To get the do-what-I-mean page motions add your own key bindings.  In
  the example below, they take the stead of ~forward-page~ (=C-x ]=) and
  ~backward-page~ (=C-x [=).  The command ~logos-narrow-dwim~ need not
  be bound, especially if you are already familiar with the various
  narrowing commands (otherwise it maps to =C-x n n= in this example,
  assuming the default keys).

+ To have quick access to ~logos-focus-mode~, bind it to a key. This
  mode checks the variables ~logos-hide-cursor~,
  ~logos-hide-mode-line~, ~logos-hide-header-line~,
  ~logos-scroll-lock~, ~logos-variable-pitch~,
  ~logos-hide-buffer-boundaries~, ~logos-buffer-read-only~,
  ~logos-olivetti~ (requires =olivetti= package) and applies their
  effects if they are non-nil. Note that everything is buffer-local,
  so it is possible to use file variables as described in the Emacs
  manual.

#+begin_src emacs-lisp
(require 'logos)

;; If you want to use outlines instead of page breaks (the ^L):
(setq logos-outlines-are-pages t)

;; This is the default value for the outlines:
(setq logos-outline-regexp-alist
      `((emacs-lisp-mode . "^;;;+ ")
        (org-mode . "^\\*+ +")
        (markdown-mode . "^\\#+ +")))

;; These apply when `logos-focus-mode' is enabled.  Their value is
;; buffer-local.
(setq-default logos-hide-cursor nil
              logos-hide-mode-line t
              logos-hide-header-line t
              logos-hide-buffer-boundaries t
              logos-hide-fringe t
              logos-variable-pitch nil
              logos-buffer-read-only nil
              logos-scroll-lock nil
              logos-olivetti nil)

;; Also check this manual for `logos-focus-mode-hook'.  It lets you
;; extend `logos-focus-mode'.

(let ((map global-map))
  (define-key map [remap narrow-to-region] #'logos-narrow-dwim)
  (define-key map [remap forward-page] #'logos-forward-page-dwim)
  (define-key map [remap backward-page] #'logos-backward-page-dwim)
  (define-key map (kbd "<f9>") #'logos-focus-mode))

;; Also consider adding keys to `logos-focus-mode-map'.  They will take
;; effect when `logos-focus-mode' is enabled.
#+end_src

* Extra tweaks
:PROPERTIES:
:CUSTOM_ID: h:2bb57369-352a-43bf-afe3-0bed2fcc7359
:END:
#+cindex: User-level configurations and glue code

This section contains snippets of code that extend the functionality of
=logos=.  These either apply to ~logos-focus-mode~ or enhance the page
motions through the ~logos-page-motion-hook~.

** Center the buffer in its window
:PROPERTIES:
:CUSTOM_ID: h:8864fb36-53d6-40a2-8e0a-2c609e06d70f
:END:
#+cindex: Automatically toggle olivetti-mode

Install the excellent =olivetti= package by Paul W. Rankin.  Then set
~logos-olivetti~ to non-nil.

The present author's favourite settings given a ~fill-column~ of =72=:

#+begin_src emacs-lisp
(setq olivetti-body-width 0.7
      olivetti-minimum-body-width 80
      olivetti-recall-visual-line-mode-entry-state t)
#+end_src

Though note that Olivetti works well even without a ~fill-column~ and
~auto-fill-mode~ disabled.

** Make EWW look like the rest of Emacs
:PROPERTIES:
:CUSTOM_ID: h:2b598775-8a3d-4728-a7ce-edc05d90fdb9
:END:

By default, all =M-x eww= buffers use the ~shr-max-width~ which is set
to 120 characters.  This is above the standard value of ~fill-column~
and thus does not let text flow nicely while using =olivetti= package
([[#h:8864fb36-53d6-40a2-8e0a-2c609e06d70f][Center the buffer in its window]]).

For a general customization, the user can evaluate this:

#+begin_src emacs-lisp
(setq shr-max-width fill-column)
#+end_src

EWW buffers also default to ~variable-pitch~ typography by default (as
opposed to whatever the font family of the ~default~ face is).  This too
can be made consistent with the rest of Emacs:

#+begin_src emacs-lisp
(setq shr-use-fonts nil)
#+end_src

[ For font-related customizations check the =fontaine= package on GNU
  ELPA (by Protesilaos). ]

Note that all variables with the =shr-= prefix are about the built-in
Simple HTML Renderer, so they will affect any other package that relies
on them beside EWW (in principle, the aforementioned should not pose any
problem).

** Automatically reveal Org or Outline subtree
:PROPERTIES:
:CUSTOM_ID: h:e18f828f-f9a8-4821-b73b-46793be57abb
:END:
#+cindex: Always show the Org or Outline subtree

The Logos page motions normally jump between positions.  Though Org and
Outline require that Logos also reveals the headings' contents.  This is
necessary to avoid invisible motions inside a folded heading that
contains subheadings.  The unfolding only applies to the current entry.
This is the relevant snippet from =logos.el=:

#+begin_src emacs-lisp
(defun logos-reveal-entry ()
  "Reveal Org or Outline entry."
  (cond
   ((and (eq major-mode 'org-mode)
         (org-at-heading-p))
    (org-show-entry))
   ((or (eq major-mode 'outline-mode)
        (bound-and-true-p outline-minor-mode))
    (outline-show-entry))))

(add-hook 'logos-page-motion-hook #'logos-reveal-entry)
#+end_src

Users may prefer to reveal the entire subtree instead of the current
entry: the heading at point and all of its subheadings.  In this case,
one may override the definition of ~logos-reveal-entry~:

#+begin_src emacs-lisp
;; glue code to expand an Org/Outline heading
(defun logos-reveal-entry ()
  "Reveal Org or Outline entry."
  (cond
   ((and (eq major-mode 'org-mode)
         (org-at-heading-p))
    (org-show-subtree))
   ((or (eq major-mode 'outline-mode)
        (bound-and-true-p outline-minor-mode))
    (outline-show-subtree))))
#+end_src

** Recenter at the top upon page motion
:PROPERTIES:
:CUSTOM_ID: h:bba965c6-7451-4c76-84d6-7e03c99ed546
:END:
#+cindex: Reposition the point at the top of the page

Page motions normally reposition the point at the centre of the window
if necessary (this is standard Emacs behaviour).  To always change the
placement invoke the ~recenter~ function with a numeric argument.

#+begin_src emacs-lisp
;; place point at the top when changing pages
(defun my-logos-recenter-top ()
  "Use `recenter' to reposition the view at the top."
  (recenter 0))

(add-hook 'logos-page-motion-hook #'my-logos-recenter-top)
#+end_src

The =0= argument refers to the topmost line.  So =1= points to the line
below and so on.

If the recentering should not affect specific modes, tweak the function
accordingly:

#+begin_src emacs-lisp
(defvar my-logos-no-recenter-top-modes 
  '(emacs-lisp-mode lisp-interaction-mode))

(defun my-logos-recenter-top ()
  "Use `recenter' to reposition the view at the top."
  (unless (memq major-mode my-logos-no-recenter-top-modes)
    (recenter 0)))
#+end_src

Or simply exclude all programming modes:

#+begin_src emacs-lisp
(defun my-logos-recenter-top ()
  "Use `recenter' to reposition the view at the top."
  (unless (derived-mode-p 'prog-mode)
    (recenter 0)))
#+end_src

** Use outlines and page breaks
:PROPERTIES:
:CUSTOM_ID: h:3464ada8-c55d-4179-9d54-c2f87e284ac7
:END:
#+cindex: Outline headings and page delimiters together

By default, the page motions only move between the =^L= delimiters.
While the option ~logos-outlines-are-pages~ changes the behaviour to
move between outline headings instead.  What constitutes an "outline
heading" is determined by the ~logos-outline-regexp-alist~ with an
automatic fallback to either ~outline-regexp~ or ~page-delimiter~
(Logos handles this fallback condition internally).

Provided this:

#+begin_src emacs-lisp
(setq logos-outlines-are-pages t)
#+end_src

The default value of ~logos-outline-regexp-alist~ will affect
~org-mode~, ~emacs-lisp-mode~, and any of their derivatives
(e.g. ~lisp-interaction-mode~ (the standard scratch buffer) is based on
~emacs-lisp-mode~).  Its fallback value is whatever the major mode sets
as an outline, else the standard =^L=.

#+begin_src emacs-lisp
(setq logos-outline-regexp-alist
      `((emacs-lisp-mode . "^;;;+ ")
        (org-mode . "^\\*+ +")))
#+end_src

It is possible to tweak those regular expressions to target both the
outline and the page delimiters:

#+begin_src emacs-lisp
(setq logos-outline-regexp-alist
      `((emacs-lisp-mode . ,(format "\\(^;;;+ \\|%s\\)" logos-page-delimiter))
        (org-mode . ,(format "\\(^\\*+ +\\|%s\\)" logos-page-delimiter))))
#+end_src

The form =,(format "\\(^;;;+ \\|%s\\)" logos-page-delimiter)=
expands to ="\\(^;;;+ \\|^^L\\)"= where =^L= is the actual control character.

For Org it may be better to either not target the =^L= or to also target
the horizontal rule (five hyphens on a line, else the =^-\\{5\\}$=
pattern).  Putting it all together:

#+begin_src emacs-lisp
(setq logos-outline-regexp-alist
      `((emacs-lisp-mode . ,(format "\\(^;;;+ \\|%s\\)" logos-page-delimiter))
        (org-mode . ,(format "\\(^\\*+ +\\|^-\\{5\\}$\\|%s\\)" logos-page-delimiter))))
#+end_src

Another Org-specific tweak is to use heading levels up to a specific
number.  The idea would be that anything below that number is not
significant.  For example, =^\\* += only applies to top-level headings,
while =^\\*\\{1,3\\} += covers heading levels 1 through 3.  Accounting
for the aforementiond horizontal rule and generic page delimiter, the
end result can look like this:

#+begin_src emacs-lisp
(setq logos-outline-regexp-alist
      `((emacs-lisp-mode . ,(format "\\(^;;;+ \\|%s\\)" logos-page-delimiter))
        (org-mode . ,(format "\\(^\\*\\{1,3\\} +\\|^-\\{5\\}$\\|%s\\)" logos-page-delimiter))))
#+end_src

** Leverage logos-focus-mode-hook
:PROPERTIES:
:CUSTOM_ID: h:a2540f2f-1159-4e5c-a486-e1f2cb63fee8
:END:
#+cindex: User-level extensions to logos-focus-mode

#+vindex: logos-focus-mode-hook
The ~logos-focus-mode-hook~ is a normal hook that runs when
~logos-focus-mode~ is enabled.  Each function is called without an
argument and looks like those in =logos.el=.  An example that sets a
variable is ~logos--buffer-read-only~; one that sets a mode is
~logos--scroll-lock~; another that sets the mode of an external
package is ~logos--olivetti~; while ~logos--hide-fringe~ provides yet
another useful sample.

If a function cannot be like the aforementioned though still needs to
set its state both when ~logos-focus-mode~ is enabled and disabled, then
use the ~logos-focus-mode-hook~ instead.

*** Conditionally toggle org-indent-mode
:PROPERTIES:
:CUSTOM_ID: h:ef719925-8d0b-479a-a87e-32b727578bfc
:END:

Here is a snippet that relies on the ~logos-focus-mode-hook~ to extend
the functionality of ~logos-focus-mode~ ([[#h:a2540f2f-1159-4e5c-a486-e1f2cb63fee8][Leverage logos-focus-mode-hook]]).

It disables ~org-indent-mode~ when ~logos-focus-mode~ is enabled and
restores it when ~logos-focus-mode~ is disabled.  The
~logos-set-mode-arg~ function takes care of the technicalities.

#+begin_src emacs-lisp
(defun my-logos-org-indent ()
  (when logos-focus-mode
    (logos-set-mode-arg 'org-indent-mode -1)))

(add-hook 'logos-focus-mode-hook #'my-logos-org-indent)
#+end_src

*** Disable menu-bar, tool-bar, tab-bar, tab-line
:PROPERTIES:
:CUSTOM_ID: h:8914f8fc-e6e9-440e-b037-f934e08ee1ae
:END:

Continuing with the examples in this section of the manual, the code
block below shows how to disable the ~menu-bar-mode~, ~tool-bar-mode~,
~tab-bar-mode~, and ~tab-line-mode~ when ~logos-focus-mode~ is
enabled.  If the modes are already disabled, the corresponding
function does nothing.  Otherwise it toggles the mode off/on when
~logos-focus-mode~ is enabled/disabled.

#+begin_src emacs-lisp
;; Assuming the `menu-bar-mode' is enabled by default...
(defun my-logos-hide-menu-bar ()
  (when logos-focus-mode
    (logos-set-mode-arg 'menu-bar-mode -1)))

(add-hook 'logos-focus-mode-hook #'my-logos-hide-menu-bar)

;; Assuming the `tool-bar-mode' is enabled by default...
(defun my-logos-hide-tool-bar ()
  (when logos-focus-mode
    (logos-set-mode-arg 'tool-bar-mode -1)))

(add-hook 'logos-focus-mode-hook #'my-logos-hide-tool-bar)

;; Assuming the `tab-bar-mode' is enabled by default...
(defun my-logos-hide-tab-bar ()
  (when logos-focus-mode
    (logos-set-mode-arg 'tab-bar-mode -1)))

(add-hook 'logos-focus-mode-hook #'my-logos-hide-tab-bar)

;; Assuming the `tab-line-mode' is enabled by default...
(defun my-logos-hide-tab-line ()
  (when logos-focus-mode
    (logos-set-mode-arg 'tab-line-mode -1)))

(add-hook 'logos-focus-mode-hook #'my-logos-hide-tab-line)
#+end_src

** Update fringe color on theme switch
:PROPERTIES:
:CUSTOM_ID: h:6a254fa0-5706-4032-8a8b-233ffb1f0e6b
:END:

The user option ~logos-hide-fringe~ does not actually remove the fringe,
as that would change the user's preference for ~fringe-mode~.  Instead,
it remaps its background color to be the same as that of the ~default~
face.  For example, if the main background is white while the fringe is
gray, the fringe will become white as well.

#+findex: logos-update-fringe-in-buffers
The problem with this approach is that the color is not automatically
updated upon switching to a new theme, such as by toggling between one
with a light background to another with a dark one.  The solution is to
assign the ~logos-update-fringe-in-buffers~ function to a hook that is
triggered by the theme-loading operation.

Some themes provide such a hook.  For example, the =modus-themes=
package has the ~modus-themes-after-load-theme-hook~ (the themes
=modus-operandi= and =modus-vivendi= are built into Emacs version 28 or
higher).

#+begin_src emacs-lisp
(add-hook 'modus-themes-after-load-theme-hook #'logos-update-fringe-in-buffers)
#+end_src

Or use the special hook ~enable-theme-functions~ that is part of
Emacs 29. [ The support of ~logos-update-fringe-in-buffers~ for
~enable-theme-functions~ is part of {{{development-version}}}. ]

#+begin_src emacs-lisp
(add-hook 'enable-theme-functions #'logos-update-fringe-in-buffers)
#+end_src

For older versions of Emacs before 29, a user-defined, theme-agnostic
setup for such a hook can be configured thus:

     #+begin_src emacs-lisp
(defvar after-enable-theme-hook nil
  "Normal hook run after enabling a theme.")

(defun run-after-enable-theme-hook (&rest _args)
  "Run `after-enable-theme-hook'."
  (run-hooks 'after-enable-theme-hook))

(advice-add 'enable-theme :after #'run-after-enable-theme-hook)
     #+end_src

Then use it like this:

#+begin_src emacs-lisp
(add-hook 'after-enable-theme-hook #'logos-update-fringe-in-buffers)
#+end_src

* Acknowledgements
:PROPERTIES:
:CUSTOM_ID: h:300c12cb-853e-4e06-9627-e1d6fd3a3a38
:END:
#+cindex: Contributors

Logos is meant to be a collective effort.  Every bit of help matters.

+ Author/maintainer :: Protesilaos Stavrou.

+ Contributions to code or the manual :: Daniel Mendler, Edgar
  Vincent, Lucy McPhail, Omar Antolín Camarena, Philip Kaludercic,
  Remco van 't Veer, and user Ypot.

+ Ideas and/or user feedback :: Daniel Mendler, Dave Abrahams, Lasse
  Lindner, Marcel Ventosa, Xiaoduan, Ypot.

* GNU Free Documentation License
:PROPERTIES:
:APPENDIX: t
:CUSTOM_ID: h:2d84e73e-c143-43b5-b388-a6765da974ea
:END:

#+texinfo: @include doclicense.texi

#+begin_export html
<pre>

                GNU Free Documentation License
                 Version 1.3, 3 November 2008


 Copyright (C) 2000, 2001, 2002, 2007, 2008 Free Software Foundation, Inc.
     <https://fsf.org/>
 Everyone is permitted to copy and distribute verbatim copies
 of this license document, but changing it is not allowed.

0. PREAMBLE

The purpose of this License is to make a manual, textbook, or other
functional and useful document "free" in the sense of freedom: to
assure everyone the effective freedom to copy and redistribute it,
with or without modifying it, either commercially or noncommercially.
Secondarily, this License preserves for the author and publisher a way
to get credit for their work, while not being considered responsible
for modifications made by others.

This License is a kind of "copyleft", which means that derivative
works of the document must themselves be free in the same sense.  It
complements the GNU General Public License, which is a copyleft
license designed for free software.

We have designed this License in order to use it for manuals for free
software, because free software needs free documentation: a free
program should come with manuals providing the same freedoms that the
software does.  But this License is not limited to software manuals;
it can be used for any textual work, regardless of subject matter or
whether it is published as a printed book.  We recommend this License
principally for works whose purpose is instruction or reference.


1. APPLICABILITY AND DEFINITIONS

This License applies to any manual or other work, in any medium, that
contains a notice placed by the copyright holder saying it can be
distributed under the terms of this License.  Such a notice grants a
world-wide, royalty-free license, unlimited in duration, to use that
work under the conditions stated herein.  The "Document", below,
refers to any such manual or work.  Any member of the public is a
licensee, and is addressed as "you".  You accept the license if you
copy, modify or distribute the work in a way requiring permission
under copyright law.

A "Modified Version" of the Document means any work containing the
Document or a portion of it, either copied verbatim, or with
modifications and/or translated into another language.

A "Secondary Section" is a named appendix or a front-matter section of
the Document that deals exclusively with the relationship of the
publishers or authors of the Document to the Document's overall
subject (or to related matters) and contains nothing that could fall
directly within that overall subject.  (Thus, if the Document is in
part a textbook of mathematics, a Secondary Section may not explain
any mathematics.)  The relationship could be a matter of historical
connection with the subject or with related matters, or of legal,
commercial, philosophical, ethical or political position regarding
them.

The "Invariant Sections" are certain Secondary Sections whose titles
are designated, as being those of Invariant Sections, in the notice
that says that the Document is released under this License.  If a
section does not fit the above definition of Secondary then it is not
allowed to be designated as Invariant.  The Document may contain zero
Invariant Sections.  If the Document does not identify any Invariant
Sections then there are none.

The "Cover Texts" are certain short passages of text that are listed,
as Front-Cover Texts or Back-Cover Texts, in the notice that says that
the Document is released under this License.  A Front-Cover Text may
be at most 5 words, and a Back-Cover Text may be at most 25 words.

A "Transparent" copy of the Document means a machine-readable copy,
represented in a format whose specification is available to the
general public, that is suitable for revising the document
straightforwardly with generic text editors or (for images composed of
pixels) generic paint programs or (for drawings) some widely available
drawing editor, and that is suitable for input to text formatters or
for automatic translation to a variety of formats suitable for input
to text formatters.  A copy made in an otherwise Transparent file
format whose markup, or absence of markup, has been arranged to thwart
or discourage subsequent modification by readers is not Transparent.
An image format is not Transparent if used for any substantial amount
of text.  A copy that is not "Transparent" is called "Opaque".

Examples of suitable formats for Transparent copies include plain
ASCII without markup, Texinfo input format, LaTeX input format, SGML
or XML using a publicly available DTD, and standard-conforming simple
HTML, PostScript or PDF designed for human modification.  Examples of
transparent image formats include PNG, XCF and JPG.  Opaque formats
include proprietary formats that can be read and edited only by
proprietary word processors, SGML or XML for which the DTD and/or
processing tools are not generally available, and the
machine-generated HTML, PostScript or PDF produced by some word
processors for output purposes only.

The "Title Page" means, for a printed book, the title page itself,
plus such following pages as are needed to hold, legibly, the material
this License requires to appear in the title page.  For works in
formats which do not have any title page as such, "Title Page" means
the text near the most prominent appearance of the work's title,
preceding the beginning of the body of the text.

The "publisher" means any person or entity that distributes copies of
the Document to the public.

A section "Entitled XYZ" means a named subunit of the Document whose
title either is precisely XYZ or contains XYZ in parentheses following
text that translates XYZ in another language.  (Here XYZ stands for a
specific section name mentioned below, such as "Acknowledgements",
"Dedications", "Endorsements", or "History".)  To "Preserve the Title"
of such a section when you modify the Document means that it remains a
section "Entitled XYZ" according to this definition.

The Document may include Warranty Disclaimers next to the notice which
states that this License applies to the Document.  These Warranty
Disclaimers are considered to be included by reference in this
License, but only as regards disclaiming warranties: any other
implication that these Warranty Disclaimers may have is void and has
no effect on the meaning of this License.

2. VERBATIM COPYING

You may copy and distribute the Document in any medium, either
commercially or noncommercially, provided that this License, the
copyright notices, and the license notice saying this License applies
to the Document are reproduced in all copies, and that you add no
other conditions whatsoever to those of this License.  You may not use
technical measures to obstruct or control the reading or further
copying of the copies you make or distribute.  However, you may accept
compensation in exchange for copies.  If you distribute a large enough
number of copies you must also follow the conditions in section 3.

You may also lend copies, under the same conditions stated above, and
you may publicly display copies.


3. COPYING IN QUANTITY

If you publish printed copies (or copies in media that commonly have
printed covers) of the Document, numbering more than 100, and the
Document's license notice requires Cover Texts, you must enclose the
copies in covers that carry, clearly and legibly, all these Cover
Texts: Front-Cover Texts on the front cover, and Back-Cover Texts on
the back cover.  Both covers must also clearly and legibly identify
you as the publisher of these copies.  The front cover must present
the full title with all words of the title equally prominent and
visible.  You may add other material on the covers in addition.
Copying with changes limited to the covers, as long as they preserve
the title of the Document and satisfy these conditions, can be treated
as verbatim copying in other respects.

If the required texts for either cover are too voluminous to fit
legibly, you should put the first ones listed (as many as fit
reasonably) on the actual cover, and continue the rest onto adjacent
pages.

If you publish or distribute Opaque copies of the Document numbering
more than 100, you must either include a machine-readable Transparent
copy along with each Opaque copy, or state in or with each Opaque copy
a computer-network location from which the general network-using
public has access to download using public-standard network protocols
a complete Transparent copy of the Document, free of added material.
If you use the latter option, you must take reasonably prudent steps,
when you begin distribution of Opaque copies in quantity, to ensure
that this Transparent copy will remain thus accessible at the stated
location until at least one year after the last time you distribute an
Opaque copy (directly or through your agents or retailers) of that
edition to the public.

It is requested, but not required, that you contact the authors of the
Document well before redistributing any large number of copies, to
give them a chance to provide you with an updated version of the
Document.


4. MODIFICATIONS

You may copy and distribute a Modified Version of the Document under
the conditions of sections 2 and 3 above, provided that you release
the Modified Version under precisely this License, with the Modified
Version filling the role of the Document, thus licensing distribution
and modification of the Modified Version to whoever possesses a copy
of it.  In addition, you must do these things in the Modified Version:

A. Use in the Title Page (and on the covers, if any) a title distinct
   from that of the Document, and from those of previous versions
   (which should, if there were any, be listed in the History section
   of the Document).  You may use the same title as a previous version
   if the original publisher of that version gives permission.
B. List on the Title Page, as authors, one or more persons or entities
   responsible for authorship of the modifications in the Modified
   Version, together with at least five of the principal authors of the
   Document (all of its principal authors, if it has fewer than five),
   unless they release you from this requirement.
C. State on the Title page the name of the publisher of the
   Modified Version, as the publisher.
D. Preserve all the copyright notices of the Document.
E. Add an appropriate copyright notice for your modifications
   adjacent to the other copyright notices.
F. Include, immediately after the copyright notices, a license notice
   giving the public permission to use the Modified Version under the
   terms of this License, in the form shown in the Addendum below.
G. Preserve in that license notice the full lists of Invariant Sections
   and required Cover Texts given in the Document's license notice.
H. Include an unaltered copy of this License.
I. Preserve the section Entitled "History", Preserve its Title, and add
   to it an item stating at least the title, year, new authors, and
   publisher of the Modified Version as given on the Title Page.  If
   there is no section Entitled "History" in the Document, create one
   stating the title, year, authors, and publisher of the Document as
   given on its Title Page, then add an item describing the Modified
   Version as stated in the previous sentence.
J. Preserve the network location, if any, given in the Document for
   public access to a Transparent copy of the Document, and likewise
   the network locations given in the Document for previous versions
   it was based on.  These may be placed in the "History" section.
   You may omit a network location for a work that was published at
   least four years before the Document itself, or if the original
   publisher of the version it refers to gives permission.
K. For any section Entitled "Acknowledgements" or "Dedications",
   Preserve the Title of the section, and preserve in the section all
   the substance and tone of each of the contributor acknowledgements
   and/or dedications given therein.
L. Preserve all the Invariant Sections of the Document,
   unaltered in their text and in their titles.  Section numbers
   or the equivalent are not considered part of the section titles.
M. Delete any section Entitled "Endorsements".  Such a section
   may not be included in the Modified Version.
N. Do not retitle any existing section to be Entitled "Endorsements"
   or to conflict in title with any Invariant Section.
O. Preserve any Warranty Disclaimers.

If the Modified Version includes new front-matter sections or
appendices that qualify as Secondary Sections and contain no material
copied from the Document, you may at your option designate some or all
of these sections as invariant.  To do this, add their titles to the
list of Invariant Sections in the Modified Version's license notice.
These titles must be distinct from any other section titles.

You may add a section Entitled "Endorsements", provided it contains
nothing but endorsements of your Modified Version by various
parties--for example, statements of peer review or that the text has
been approved by an organization as the authoritative definition of a
standard.

You may add a passage of up to five words as a Front-Cover Text, and a
passage of up to 25 words as a Back-Cover Text, to the end of the list
of Cover Texts in the Modified Version.  Only one passage of
Front-Cover Text and one of Back-Cover Text may be added by (or
through arrangements made by) any one entity.  If the Document already
includes a cover text for the same cover, previously added by you or
by arrangement made by the same entity you are acting on behalf of,
you may not add another; but you may replace the old one, on explicit
permission from the previous publisher that added the old one.

The author(s) and publisher(s) of the Document do not by this License
give permission to use their names for publicity for or to assert or
imply endorsement of any Modified Version.


5. COMBINING DOCUMENTS

You may combine the Document with other documents released under this
License, under the terms defined in section 4 above for modified
versions, provided that you include in the combination all of the
Invariant Sections of all of the original documents, unmodified, and
list them all as Invariant Sections of your combined work in its
license notice, and that you preserve all their Warranty Disclaimers.

The combined work need only contain one copy of this License, and
multiple identical Invariant Sections may be replaced with a single
copy.  If there are multiple Invariant Sections with the same name but
different contents, make the title of each such section unique by
adding at the end of it, in parentheses, the name of the original
author or publisher of that section if known, or else a unique number.
Make the same adjustment to the section titles in the list of
Invariant Sections in the license notice of the combined work.

In the combination, you must combine any sections Entitled "History"
in the various original documents, forming one section Entitled
"History"; likewise combine any sections Entitled "Acknowledgements",
and any sections Entitled "Dedications".  You must delete all sections
Entitled "Endorsements".


6. COLLECTIONS OF DOCUMENTS

You may make a collection consisting of the Document and other
documents released under this License, and replace the individual
copies of this License in the various documents with a single copy
that is included in the collection, provided that you follow the rules
of this License for verbatim copying of each of the documents in all
other respects.

You may extract a single document from such a collection, and
distribute it individually under this License, provided you insert a
copy of this License into the extracted document, and follow this
License in all other respects regarding verbatim copying of that
document.


7. AGGREGATION WITH INDEPENDENT WORKS

A compilation of the Document or its derivatives with other separate
and independent documents or works, in or on a volume of a storage or
distribution medium, is called an "aggregate" if the copyright
resulting from the compilation is not used to limit the legal rights
of the compilation's users beyond what the individual works permit.
When the Document is included in an aggregate, this License does not
apply to the other works in the aggregate which are not themselves
derivative works of the Document.

If the Cover Text requirement of section 3 is applicable to these
copies of the Document, then if the Document is less than one half of
the entire aggregate, the Document's Cover Texts may be placed on
covers that bracket the Document within the aggregate, or the
electronic equivalent of covers if the Document is in electronic form.
Otherwise they must appear on printed covers that bracket the whole
aggregate.


8. TRANSLATION

Translation is considered a kind of modification, so you may
distribute translations of the Document under the terms of section 4.
Replacing Invariant Sections with translations requires special
permission from their copyright holders, but you may include
translations of some or all Invariant Sections in addition to the
original versions of these Invariant Sections.  You may include a
translation of this License, and all the license notices in the
Document, and any Warranty Disclaimers, provided that you also include
the original English version of this License and the original versions
of those notices and disclaimers.  In case of a disagreement between
the translation and the original version of this License or a notice
or disclaimer, the original version will prevail.

If a section in the Document is Entitled "Acknowledgements",
"Dedications", or "History", the requirement (section 4) to Preserve
its Title (section 1) will typically require changing the actual
title.


9. TERMINATION

You may not copy, modify, sublicense, or distribute the Document
except as expressly provided under this License.  Any attempt
otherwise to copy, modify, sublicense, or distribute it is void, and
will automatically terminate your rights under this License.

However, if you cease all violation of this License, then your license
from a particular copyright holder is reinstated (a) provisionally,
unless and until the copyright holder explicitly and finally
terminates your license, and (b) permanently, if the copyright holder
fails to notify you of the violation by some reasonable means prior to
60 days after the cessation.

Moreover, your license from a particular copyright holder is
reinstated permanently if the copyright holder notifies you of the
violation by some reasonable means, this is the first time you have
received notice of violation of this License (for any work) from that
copyright holder, and you cure the violation prior to 30 days after
your receipt of the notice.

Termination of your rights under this section does not terminate the
licenses of parties who have received copies or rights from you under
this License.  If your rights have been terminated and not permanently
reinstated, receipt of a copy of some or all of the same material does
not give you any rights to use it.


10. FUTURE REVISIONS OF THIS LICENSE

The Free Software Foundation may publish new, revised versions of the
GNU Free Documentation License from time to time.  Such new versions
will be similar in spirit to the present version, but may differ in
detail to address new problems or concerns.  See
https://www.gnu.org/licenses/.

Each version of the License is given a distinguishing version number.
If the Document specifies that a particular numbered version of this
License "or any later version" applies to it, you have the option of
following the terms and conditions either of that specified version or
of any later version that has been published (not as a draft) by the
Free Software Foundation.  If the Document does not specify a version
number of this License, you may choose any version ever published (not
as a draft) by the Free Software Foundation.  If the Document
specifies that a proxy can decide which future versions of this
License can be used, that proxy's public statement of acceptance of a
version permanently authorizes you to choose that version for the
Document.

11. RELICENSING

"Massive Multiauthor Collaboration Site" (or "MMC Site") means any
World Wide Web server that publishes copyrightable works and also
provides prominent facilities for anybody to edit those works.  A
public wiki that anybody can edit is an example of such a server.  A
"Massive Multiauthor Collaboration" (or "MMC") contained in the site
means any set of copyrightable works thus published on the MMC site.

"CC-BY-SA" means the Creative Commons Attribution-Share Alike 3.0
license published by Creative Commons Corporation, a not-for-profit
corporation with a principal place of business in San Francisco,
California, as well as future copyleft versions of that license
published by that same organization.

"Incorporate" means to publish or republish a Document, in whole or in
part, as part of another Document.

An MMC is "eligible for relicensing" if it is licensed under this
License, and if all works that were first published under this License
somewhere other than this MMC, and subsequently incorporated in whole or
in part into the MMC, (1) had no cover texts or invariant sections, and
(2) were thus incorporated prior to November 1, 2008.

The operator of an MMC Site may republish an MMC contained in the site
under CC-BY-SA on the same site at any time before August 1, 2009,
provided the MMC is eligible for relicensing.


ADDENDUM: How to use this License for your documents

To use this License in a document you have written, include a copy of
the License in the document and put the following copyright and
license notices just after the title page:

    Copyright (c)  YEAR  YOUR NAME.
    Permission is granted to copy, distribute and/or modify this document
    under the terms of the GNU Free Documentation License, Version 1.3
    or any later version published by the Free Software Foundation;
    with no Invariant Sections, no Front-Cover Texts, and no Back-Cover Texts.
    A copy of the license is included in the section entitled "GNU
    Free Documentation License".

If you have Invariant Sections, Front-Cover Texts and Back-Cover Texts,
replace the "with...Texts." line with this:

    with the Invariant Sections being LIST THEIR TITLES, with the
    Front-Cover Texts being LIST, and with the Back-Cover Texts being LIST.

If you have Invariant Sections without Cover Texts, or some other
combination of the three, merge those two alternatives to suit the
situation.

If your document contains nontrivial examples of program code, we
recommend releasing these examples in parallel under your choice of
free software license, such as the GNU General Public License,
to permit their use in free software.
</pre>
#+end_export

#+html: <!--

* Indices
:PROPERTIES:
:CUSTOM_ID: h:0325b677-0b1b-426e-a5d5-ddc225fde6fa
:END:

** Function index
:PROPERTIES:
:INDEX: fn
:CUSTOM_ID: h:40430725-fd7f-47ac-9a29-913942e84a57
:END:

** Variable index
:PROPERTIES:
:INDEX: vr
:CUSTOM_ID: h:91f3c207-8149-4f9a-89cf-b8726e4e4415
:END:

** Concept index
:PROPERTIES:
:INDEX: cp
:CUSTOM_ID: h:2b11517a-b67f-494f-b111-1c6195e8a2fc
:END:

#+html: -->
